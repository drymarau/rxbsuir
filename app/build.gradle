apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'hu.supercluster.paperwork'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'

// Manifest version information
def versionMajor = 1
def versionMinor = 4
def versionPatch = 4
def versionBuild = 0

android {
  compileSdkVersion versions.compileSdk
  buildToolsVersion versions.buildTools

  defaultConfig {
    applicationId 'by.toggi.rxbsuir'
    minSdkVersion versions.minSdk
    targetSdkVersion versions.targetSdk
    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    resConfigs "en", "ru"

    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  testOptions {
    execution 'ANDROID_TEST_ORCHESTRATOR'
  }
  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
    androidTest.java.srcDirs += 'src/androidTest/kotlin'
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  implementation libraries.kotlin.stdlib

  implementation libraries.support.appcompat
  implementation libraries.support.recyclerview
  implementation libraries.support.design
  implementation libraries.support.cardview

  implementation libraries.butterknife.butterknife
  kapt libraries.butterknife.compiler

  implementation libraries.timber
  implementation libraries.threetenabp

  implementation libraries.rxbinding.rxbinding
  implementation libraries.rxbinding.appcompat

  implementation libraries.okhttp.okhttp
  implementation libraries.okhttp.logging

  implementation libraries.retrofit.retrofit
  implementation libraries.retrofit.adapters.rxjava
  implementation libraries.retrofit.converters.gson

  debugImplementation libraries.leakcanary.leakcanary
  releaseImplementation libraries.leakcanary.noop

  implementation libraries.rxjava
  implementation libraries.rxandroid

  implementation libraries.rxlifecycle.rxlifecycle
  implementation libraries.rxlifecycle.components

  implementation libraries.rxpreferences

  implementation(libraries.materialdialogs.core) {
    exclude module: 'support-v13'
  }
  implementation(libraries.materialdialogs.commons) {
    exclude module: 'support-v13'
  }

  implementation libraries.preferencesfix.preferencesfix
  implementation libraries.preferencesfix.datetimepicker

  implementation libraries.storio.sqlite
  implementation libraries.storio.sqliteannotations
  kapt libraries.storio.sqliteannotationsprocessor

  implementation libraries.dagger.dagger
  implementation libraries.dagger.android
  implementation libraries.dagger.support
  kapt libraries.dagger.compiler
  kapt libraries.dagger.processor
  compileOnly libraries.javax.inject

  implementation libraries.parceler.api
  kapt libraries.parceler.parceler

  implementation(libraries.crashlytics) {
    transitive = true
  }

  implementation libraries.paperwork

  testImplementation libraries.junit

  testImplementation libraries.okhttp.mockwebserver

  testImplementation libraries.mockito.core

  testImplementation libraries.assertj.core

  androidTestImplementation libraries.supporttest.runner
  androidTestUtil libraries.supporttest.orchestrator

  androidTestImplementation libraries.assertj.core
}

paperwork {
  set = [gitSha   : gitSha(),
         gitTag   : gitTag(),
         gitInfo  : gitInfo(),
         buildTime: buildTime("yyyy-MM-dd'T'HH:mm:ss'Z'", 'UTC')]
}

repositories {
  maven { url 'https://maven.fabric.io/public' }
}

buildscript {
  repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath gradlePlugins.fabric
    classpath gradlePlugins.dexcount
    classpath gradlePlugins.paperwork
  }
}
